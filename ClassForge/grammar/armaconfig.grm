! ----------------------------------------------------------------------------
! Arma 3 Configuration File Grammar
!
! The C# Programming Language was created by the Microsoft Corporation to be 
! used with the .NET platform. The goal of C# was to counter the flaws and 
! difficulties found in Java and C++. In addition, the language was designed
! to work well with the Common Language Runtime which runs the .NET platform.
!
! ----------------------------------------------------------------------------

"Name"     = 'ArmaConfig'
"Version"  = '1.0'
"Author"   = 'Alex Vorobiev'           

"About"    = 'This grammar is based on C# grammar that was created by Anders Hejlsberg.'

"Start Symbol" = <Class Decl>

! ----------------------------------------------------------------- Sets

{ID Head}        = {Letter} + [_]
{ID Tail}        = {AlphaNumeric} + [_]
{String Ch}      = {Printable} - ["]

! ----------------------------------------------------------------- Terminals
Identifier     = [@]? {ID Head} {ID Tail}*        !The @ is an override char

DecLiteral     = {Digit}+            ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
RealLiteral    = {Digit}*'.'{Digit}+

StringLiteral  = '"'( {String Ch} | '\'{Printable} )* '"'

Arguments = '(' {String Ch}+ ')'

! ----------------------------------------------------------------- Comments

Comment Line = '//'
Comment Start = '/*'
Comment End = '*/'

<Semicolon Opt>
       ::= ';'
        |  !Nothing 

! ===========================================================================
! C.1.8 Literals 
! ===========================================================================

<Literal>
       ::= true 
        |  false
        |  DecLiteral
        |  RealLiteral
        |  StringLiteral
        |  Identifier Arguments
        |  Identifier
             
<Rank Specifier>
       ::= '[]'
             
! ===========================================================================
! C.2.7 Classes
! ===========================================================================

<Class Decl>
       ::= class Identifier <Class Base Opt> '{' <Class Item Decs Opt> '}' <Semicolon Opt>

<Class Base Opt>
       ::= ':' <Class Base List>
        |  !Nothing 

<Class Base List>
       ::= Identifier

<Class Item Decs Opt>
       ::= <Class Item Decs Opt> <Class Item>
        |  !Nothing 

<Class Item>
       ::= <Param Dec>
        | <Class Decl>
        
<Param Dec>
       ::= <Variable Declarator> ';'
        | <Array Declarator> ';'
        | <Additive Array Declarator> ';'
        
<Variable Declarator>
        ::= Identifier
         |  Identifier '=' <Literal>

<Array Declarator>
        ::= Identifier <Rank Specifier> '=' '{' <Literal List Opt> '}'
         
<Additive Array Declarator>
        ::= Identifier <Rank Specifier> '+=' '{' <Literal List Opt> '}'
           
<Literal List Opt>
        ::= <Literal List>
         | !Nothing
         
<Literal List>
        ::= <Literal List> ',' <Literal>
         | <Literal>

